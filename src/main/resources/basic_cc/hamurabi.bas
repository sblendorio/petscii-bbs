  50 RANDOMIZE TIMER
 100 PRINT TAB(32);"HAMURABI"
 200 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
 300 PRINT:PRINT:PRINT
 800 PRINT "TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA"
 900 PRINT "FOR A TEN-YEAR TERM OF OFFICE.":PRINT
 950 D1=0: P1=0
1000 Z=0: P=95:S=2800: H=3000: E=H-S
1100 Y=3: A=H/Y: I=5: Q=1
2100 D=0
2150 PRINT:PRINT:PRINT "HAMURABI:  I BEG TO REPORT TO YOU,": Z=Z+1
2170 PRINT "IN YEAR";Z;",";D;"PEOPLE STARVED,";I;"CAME TO THE CITY,"
2180 P=P+I
2270 IF Q>0 THEN 2300
2280 P=INT(P/2)
2290 PRINT "A HORRIBLE PLAGUE STRUCK!  HALF THE PEOPLE DIED."
2300 PRINT "POPULATION IS NOW";P
2320 PRINT "THE CITY NOW OWNS ";A;"ACRES."
2350 PRINT "YOU HARVESTED";Y;"BUSHELS PER ACRE."
2500 PRINT "THE RATS ATE";E;"BUSHELS."
2600 PRINT "YOU NOW HAVE ";S;"BUSHELS IN STORE.": PRINT
2700 IF Z=11 THEN 8600
3100 C=INT(10*RND(1)): Y=C+17
3120 PRINT "LAND IS TRADING AT";Y;"BUSHELS PER ACRE."
3200 PRINT "HOW MANY ACRES DO YOU WISH TO BUY";
3210 INPUT ZZ$: Q=VAL(ZZ$): IF ZZ$="." THEN GOSUB 10000 : GOTO 3210
3215 IF Q<0 THEN 8500
3220 IF Y*Q<=S THEN 3300
3230 GOSUB 7100
3240 GOTO 3200
3300 IF Q=0 THEN 3400
3310 A=A+Q: S=S-Y*Q: C=0
3340 GOTO 4000
3400 PRINT "HOW MANY ACRES DO YOU WISH TO SELL";
3410 INPUT ZZ$: Q=VAL(ZZ$): IF ZZ$="." THEN GOSUB 10000 : GOTO 3410
3415 IF Q<0 THEN 8500
3420 IF Q<A THEN 3500
3430 GOSUB 7200
3440 GOTO 3400
3500 A=A-Q: S=S+Y*Q: C=0
4000 PRINT
4100 PRINT "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE";
4110 INPUT ZZ$: Q=VAL(ZZ$): IF ZZ$="." THEN GOSUB 10000 : GOTO 4110
4120 IF Q<0 THEN 8500
4180 REM *** TRYING TO USE MORE GRAIN THAN IS IN SILOS?
4200 IF Q<=S THEN 4300
4210 GOSUB 7100
4220 GOTO 4100
4300 S=S-Q: C=1: PRINT
4400 PRINT "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED";
4410 INPUT ZZ$: D=VAL(ZZ$): IF ZZ$="." THEN GOSUB 10000 : GOTO 4110
4415 IF D=0 THEN 5110
4420 IF D<0 THEN 8500
4440 REM *** TRYING TO PLANT MORE ACRES THAN YOU OWN?
4450 IF D<=A THEN 4500
4460 GOSUB 7200
4470 GOTO 4400
4490 REM *** ENOUGH GRAIN FOR SEED?
4500 IF INT(D/2)<=S THEN 4550
4520 GOSUB 7100
4530 GOTO 4400
4540 REM *** ENOUGH PEOPLE TO TEND THE CROPS?
4550 IF D<10*P THEN 5100
4600 PRINT "BUT YOU HAVE ONLY";P;"PEOPLE TO TEND THE FIELDS!  NOW THEN,"
4700 GOTO 4400
5100 S=S-INT(D/2)
5110 GOSUB 8000
5120 REM *** A BOUNTIFUL HARVEST!
5150 Y=C: H=D*Y: E=0
5210 GOSUB 8000
5220 IF INT(C/2)<>C/2 THEN 5300
5230 REM *** RATS ARE RUNNING WILD!!
5250 E=INT(S/C)
5300 S=S-E+H
5310 GOSUB 8000
5320 REM *** LET'S HAVE SOME BABIES
5330 I=INT(C*(20*A+S)/P/100+1)
5390 REM *** HOW MANY PEOPLE HAD FULL TUMMIES?
5400 C=INT(Q/20)
5410 REM *** HORROS, A 15% CHANCE OF PLAGUE
5420 Q=INT(10*(2*RND(1)-.3))
5500 IF P<C THEN 2100
5510 REM *** STARVE ENOUGH FOR IMPEACHMENT?
5520 D=P-C: IF D>.45*P THEN 5600
5530 P1=((Z-1)*P1+D*100/P)/Z
5550 P=C: D1=D1+D: GOTO 2150
5600 PRINT: PRINT "YOU STARVED";D;"PEOPLE IN ONE YEAR!!!"
5650 PRINT "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
5660 PRINT "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU HAVE"
5670 PRINT "ALSO BEEN DECLARED NATIONAL FINK!!!!": GOTO 9900
7100 PRINT "HAMURABI:  THINK AGAIN.  YOU HAVE ONLY"
7110 PRINT S;"BUSHELS OF GRAIN.  NOW THEN,"
7120 RETURN
7200 PRINT "HAMURABI:  THINK AGAIN.  YOU OWN ONLY";A;"ACRES.  NOW THEN,"
7300 RETURN
8000 C=INT(RND(1)*5)+1
8010 RETURN
8500 PRINT: PRINT "HAMURABI:  I CANNOT DO WHAT YOU WISH."
8550 PRINT "GET YOURSELF ANOTHER STEWARD!!!!!"
8570 GOTO 9900
8600 PRINT "IN YOUR 10-YEAR TERM OF OFFICE,";P1;"PERCENT OF THE"
8620 PRINT "POPULATION STARVED PER YEAR ON THE AVERAGE, I.E. A TOTAL OF"
8650 PRINT D1;"PEOPLE DIED!!": L=A/P
8700 PRINT "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
8750 PRINT L;"ACRES PER PERSON.": PRINT
8800 IF P1>33 THEN 5650
8850 IF L<7 THEN 5650
8900 IF P1>10 THEN 9400
8920 IF L<9 THEN 9400
8950 IF P1>3 THEN 9600
8960 IF L<10 THEN 9600
9000 PRINT "A FANTASTIC PERFORMANCE!!!  CHARLEMANGE, DISRAELI, AND"
9050 PRINT "JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!":GOTO 9900
9400 PRINT "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV."
9450 PRINT "THE PEOPLE (REMIANING) FIND YOU AN UNPLEASANT RULER, AND,"
9500 PRINT "FRANKLY, HATE YOUR GUTS!!":GOTO 9900
9600 PRINT "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT"
9650 PRINT "REALLY WASN'T TOO BAD AT ALL. ";INT(P*.8*RND(1));"PEOPLE"
9700 PRINT "WOULD DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR"
9750 PRINT "TRIVIAL PROBLEMS."
9900 PRINT: FOR N=1 TO 10: PRINT CHR$(7);: NEXT N
9950 PRINT "SO LONG FOR NOW.": PRINT
9990 INPUT "PRESS ENTER TO EXIT"; ZZ$
9999 END
10000 INPUT "QUIT: ARE YOU SURE";KK$
10010 IF KK$<>"Y" AND KK$<>"S" AND KK$<>"." THEN RETURN
10020 END
