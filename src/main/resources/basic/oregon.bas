5 RANDOMIZE TIMER
10 REM O R E G O N ** 1/1/76
20 REM ORIGINAL PROGRAMMING BY BILL HEINEMANN - 1971
30 REM SUPPORT AND RESEARCH MATERIAL BY DON RAWITSCH
60 REM ADAPTED FOR APPLE-1 BY CLAUDIO PARMIGIANI - 2024
61 REM ADAPTED FOR BBS-BASIC BY FRANCESCO SBLENDORIO - 2024
70 A=0:B=0:B1=0:B3=0:C=0:C1=0:D1=0:D3=0
75 D4=0:D9=0:E=0:F=0:F1=0:F2=0:F9=0:K8=0:L1=0:M=0:M1=0:M2=0:M9=0
80 P=0:R1=0:S4=0:S5=0:S6=0:T=0:T1=0:X=0
85 DIM D$(10)
90 GOSUB 8000
110 REM
120 PRINT TAB(9);"O R E G O N   T R A I L"
130 FOR I=1 TO 800
135 IF I<=18 THEN PRINT
140 NEXT I
150 REM GOSUB 8000
160 PRINT "DO YOU NEED INSTRUCTIONS (Y/N)";
190 GOSUB 10000: C$ = ZZ$
200 IF C$="N" THEN 710
210 REM INSTRUCTIONS
220 GOSUB 8000
240 PRINT "THIS PROGRAM SIMULATES A TRIP OVER THE"
245 PRINT "OREGON TRAIL FROM INDEPENDENCE, MISS-"
250 PRINT "OURI TO OREGON CITY, OREGON IN 1847."
255 PRINT "YOUR FAMILY OF FIVE WILL COVER THE 2040"
260 PRINT "MILE OREGON TRAIL IN 5-6 MONTHS --- IF"
265 PRINT "YOU MAKE IT ALIVE."
266 PRINT : PRINT : PRINT : PRINT
268 PRINT "TYPE 'GO' TO CONTINUE: ";:GOSUB 10000: C$ = ZZ$
270 GOSUB 8000
275 PRINT "YOU HAD SAVED $900 TO SPEND FOR THE"
280 PRINT "TRIP, AND YOU'VE JUST PAID $200 FOR A"
285 PRINT "WAGON. YOU WILL NEED TO SPEND THE REST"
290 PRINT "OF YOUR MONEY ON THE FOLLOWING ITEMS:"
295 PRINT
300 PRINT TAB(4);"OXEN - YOU CAN SPEND $200-$300 ON"
305 PRINT TAB(8);"YOUR TEAM. THE MORE YOU SPEND"
310 PRINT TAB(8);"THE FASTER YOU'LL GO BECAUSE"
315 PRINT TAB(8);"YOU'LL HAVE BETTER ANIMALS"
325 PRINT TAB(4);"FOOD - THE MORE YOU HAVE THE LESS"
330 PRINT TAB(8);"CHANCE THERE IS OF GETTING SICK"
340 PRINT TAB(4);"AMMUNITION - $1 BUYS A BELT OF 50"
345 PRINT TAB(8);"BULLETS"
355 PRINT TAB(4);"CLOTHING - THIS IS VERY IMPORTANT"
360 PRINT TAB(8);"FOR THE COLD YOU WILL ENCOUNTER"
365 PRINT TAB(8);"IN THE MOUNTAINS"
375 PRINT TAB(4);"MISCELLANEOUS SUPPLIES - THIS COVERS"
380 PRINT TAB(8);"ALL THE THINGS YOU WILL NEED FOR"
385 PRINT TAB(8);"SICKNESS AND EMERGENCY REPAIRS"
388 PRINT : PRINT : PRINT "TYPE 'GO' TO CONTINUE: ";
390 GOSUB 10000: C$ = ZZ$
392 GOSUB 8000
395 PRINT "YOU CAN SPEND ALL YOUR MONEY BEFORE YOU"
400 PRINT "START OR SAVE SOME TO SPEND AT FORTS"
405 PRINT "ALONG THE WAY. HOWEVER, COSTS ARE "
410 PRINT "HIGHER AT THE FORTS."
415 PRINT
420 PRINT "YOU CAN USE YOUR RIFLE TO HUNT FOOD"
425 PRINT "ALONG THE WAY. WHENEVER YOU HAVE TO"
430 PRINT "USE YOUR TRUSTY RIFLE ALONG THE WAY,"
435 PRINT "YOU WILL BE TOLD TO TYPE IN A WORD"
440 PRINT "(ONE THAT SOUNDS LIKE A GUN SHOT)."
445 PRINT "THE FASTER YOU TYPE IN THAT WORD AND"
450 PRINT "AND HIT THE 'RETURN' KEY, THE BETTER"
455 PRINT "LUCK YOU WILL HAVE WITH YOUR GUN."
460 PRINT
465 PRINT "AT EACH TURN, ALL ITEMS SHOWN ARE IN"
470 PRINT "DOLLAR AMOUNTS EXCEPT BULLETS."
475 PRINT "WHEN ASKED TO ENTER DOLLAR AMOUNTS,"
480 PRINT "DON'T USE A '$'."
485 PRINT : PRINT
495 PRINT TAB(13);"GOOD LUCK!!"
505 PRINT : PRINT
510 PRINT "TYPE 'GO' TO BEGIN THE GAME: ";:GOSUB 10000: C$ = ZZ$
710 PRINT "HOW GOOD A SHOT ARE YOU WITH YOUR RIFLE?"
715 PRINT
720 PRINT " 1) ACE MARKSMAN"
725 PRINT " 2) GOOD SHOT"
730 PRINT " 3) FAIR TO MIDDLIN'"
735 PRINT " 4) NEED MORE PRACTICE"
740 PRINT " 5) SHAKY KNEES"
745 PRINT : PRINT
750 PRINT "ENTER ONE OF THE ABOVE -- THE BETTER"
755 PRINT "YOU CLAIM YOU ARE THE FASTER YOU'LL"
760 PRINT "HAVE TO BE WITH YOUR GUN TO SUCCEED."
765 GOSUB 10000 : D9 = VAL(ZZ$)
770 IF D9>5 THEN 790
780 GOTO 810
790 D9=0
800 REM *** INITIAL PURCHASES ***
810 X1=-1
815 K8=S4=F1=F2=M=M9=D3=0
828 PRINT
830 PRINT "HOW MUCH DO YOU WANT TO SPEND ON YOUR"
840 PRINT "OXEN TEAM";
850 GOSUB 10000: A = VAL(ZZ$)
860 IF A>=200 THEN 900
870 PRINT : PRINT "NOT ENOUGH"
880 GOTO 828
900 IF A<=300 THEN 925
910 PRINT : PRINT "TOO MUCH"
920 GOTO 828
925 PRINT
930 PRINT "HOW MUCH DO YOU WANT TO SPEND ON"
931 PRINT "FOOD";
940 GOSUB 10000: F = VAL(ZZ$)
950 IF F>=0 THEN 975
960 PRINT : PRINT "IMPOSSIBLE"
970 GOTO 925
975 PRINT
980 PRINT "HOW MUCH DO YOU WANT TO SPEND ON"
985 PRINT "AMMUNITION";
990 GOSUB 10000 : B = VAL(ZZ$)
1000 IF B>=0 THEN 1025
1001 FOR I=1 TO 169:T(I)=0: NEXT I
1010 PRINT : PRINT "IMPOSSIBLE"
1020 GOTO 975
1025 PRINT
1030 PRINT "HOW MUCH DO YOU WANT TO SPEND ON"
1035 PRINT "CLOTHING";
1040 GOSUB 10000 : C = VAL(ZZ$)
1050 IF C>=0 THEN 1075
1060 PRINT : PRINT "IMPOSSIBLE"
1070 GOTO 1025
1075 PRINT
1080 PRINT "HOW MUCH DO YOU WANT DO SPEND ON"
1085 PRINT "MISCELLANEOUS SUPPLIES";
1090 GOSUB 10000 : M1 = VAL(ZZ$)
1100 IF M1>=0 THEN 1130
1110 PRINT : PRINT "IMPOSSIBLE"
1120 GOTO 1075
1130 T=700-A-F-B-C-M1
1140 IF T>=0 THEN 1170
1150 PRINT : PRINT "YOU OVERSPENT -- YOU ONLY HAD $700"
1155 PRINT "TO SPEND. BUY AGAIN."
1160 FOR I=1 TO 1500: NEXT I
1165 GOTO 828
1170 B=50*B
1175 PRINT : PRINT
1180 PRINT "AFTER ALL YOUR PURCHASES, YOU NOW HAVE"
1185 PRINT MID$(STR$(T),2);" DOLLARS LEFT."
1190 FOR I=1 TO 2000: NEXT I
1210 PRINT
1220 GOTO 1750
1230 IF M>=2040 THEN 5430
1240 REM *** STARTING DATE ***
1250 D3=D3+1
1255 PRINT : REM PRINT
1260 ON D3 GOTO 1270,1280,1290,1300,1310,1320,1330,1340,1350,1360,1370,1380,1390,1400,1410,1420,1430,1440,1450
1270 PRINT TAB(15);" APRIL 12 1847"
1275 GOTO 1740
1280 PRINT TAB(15);" APRIL 26 1847"
1285 GOTO 1740
1290 PRINT TAB(15);" MAY 10 1847"
1295 GOTO 1740
1300 PRINT TAB(15);" MAY 24 1847"
1305 GOTO 1740
1310 PRINT TAB(15);" JUNE 7 1847"
1315 GOTO 1740
1320 PRINT TAB(15);" JUNE 21 1847"
1325 GOTO 1740
1330 PRINT TAB(15);" JULY 5 1847"
1335 GOTO 1740
1340 PRINT TAB(15);" JULY 19 1847"
1345 GOTO 1740
1350 PRINT TAB(15);" AUGUST 2 1847"
1355 GOTO 1740
1360 PRINT TAB(15);" AUGUST 16 1847"
1365 GOTO 1740
1370 PRINT TAB(15);" AUGUST 31 1847"
1375 GOTO 1740
1380 PRINT TAB(15);" SEPTEMBER 13 1847"
1385 GOTO 1740
1390 PRINT TAB(15);" SEPTEMBER 27 1847"
1395 GOTO 1740
1400 PRINT TAB(15);" OCTOBER 11 1847"
1405 GOTO 1740
1410 PRINT TAB(15);" OCTOBER 25 1847"
1415 GOTO 1740
1420 PRINT TAB(15);" NOVEMBER 8 1847"
1425 GOTO 1740
1430 PRINT TAB(15);" NOVEMBER 22 1847"
1435 GOTO 1740
1440 PRINT TAB(15);" DECEMBER 6 1847"
1445 GOTO 1740
1450 PRINT TAB(15);" DECEMBER 20 1847"
1455 GOTO 1740
1460 PRINT "YOU HAVE BEEN ON THE TRAIL TOO LONG"
1470 PRINT "YOU HAVE BEEN ON THE TRAIL TOO LONG ---"
1480 PRINT : PRINT "YOUR FAMILY DIES IN THE FIRST BLIZZARD"
1490 PRINT : PRINT "OF WINTER"
1500 GOTO 5180
1740 REM *** BEGINNING EACH TURN ***
1750 IF F>=0 THEN 1770
1760 F=0
1770 IF B>=0 THEN 1790
1780 B=0
1790 IF C>=0 THEN 1810
1800 C=0
1810 IF M1>=0 THEN 1830
1820 M1=0
1830 IF F>=13 THEN 1850
1840 PRINT "YOU'D BETTER DO SOME HUNTING OR BUY"
1845 PRINT "FOOD AND SOON!!!"
1850 M2=M
1920 IF S4=1 THEN 1950
1930 IF K8=1 THEN 1950
1940 GOTO 1990
1950 T=T-20
1960 IF T<0 THEN 5080
1970 PRINT : PRINT : PRINT "DOCTOR'S BILL IS $20"
1980 K8=0:S4=0
1990 IF M9=1 THEN M=950
2000 GOSUB 7060
2030 M9=0
2060 IF X1=-1 THEN 2170
2070 X1=X1*(-1)
2080 PRINT : PRINT : PRINT "DO YOU WANT TO (1) STOP AT THE NEXT"
2090 PRINT "FORT, (2) HUNT, OR (3) CONTINUE ";
2100 GOSUB 10000 : X = VAL(ZZ$)
2110 IF X<1 OR X>2 THEN X=3
2120 GOTO 2270
2170 PRINT : PRINT "DO YOU WANT TO (1) HUNT OR (2) CONTINUE"
2180 GOSUB 10000 : X = VAL(ZZ$)
2190 IF X=1 THEN 2210
2200 X=2
2210 LET X=X+1
2220 IF X=3 THEN 2260
2230 IF B>39 THEN 2260
2240 PRINT : PRINT "TOUGH LUCK - - - YOU NEED MORE BULLETS"
2245 PRINT "TO GO HUNTING."
2250 GOTO 2170
2260 X1=X1*(-1)
2270 IF X=1 THEN 2290
2275 IF X=2 THEN 2540
2280 IF X=3 THEN 2725
2285 REM *** STOPPING AT FORT ***
2290 PRINT : PRINT : PRINT "HOW MUCH DO YOU WANT TO SPEND ON THE"
2295 PRINT "FOLLOWING?"
2300 PRINT : PRINT "FOOD ";
2310 GOSUB 7200
2410 F=F+INT(P*2/3)
2415 GOSUB 7120
2420 PRINT "AMMUNITION";
2430 GOSUB 7200
2440 LET B=B+INT((2*50*P)/3)
2445 GOSUB 7120
2450 PRINT "CLOTHING";
2460 GOSUB 7200
2470 C=C+INT(2*P/3)
2475 GOSUB 7120
2480 PRINT "MISCELLANEOUS SUPPLIES ";
2490 GOSUB 7200
2500 M1=M1+INT(2*P/3)
2510 M=M-45
2515 GOSUB 7120
2520 GOTO 2725
2530 REM *** HUNTING ***
2540 IF B>39 THEN 2570
2550 PRINT : PRINT "TOUGH -- YOU DON'T HAVE ENOUGH AMMO"
2560 GOTO 2080
2570 M=M-45
2580 GOSUB 6140
2590 IF (B1<=10 AND Z=S6) THEN 2660
2600 IF ((INT(RND(1)*100)<5*B1)OR (Z<>S6)) THEN 2710
2610 F=F+48-2*B1
2620 PRINT : PRINT "NICE SHOT -- GOOD EATIN' TONIGHT!!"
2630 B=B-10-3*B1
2640 GOTO 2725
2650 REM *** BELLS IN LINE 2660 ***
2660 PRINT : PRINT "RIGHT BETWEEN THE EYES - IT'S A BIG ONE"
2670 PRINT "FULL BELLIES TONIGHT!!"
2680 F=F+52+ INT(RND(1)*6)
2690 B=B-10- INT(RND(1)*4)
2700 GOTO 2725
2710 PRINT "YOU MISSED -- AND DINNER GOT AWAY---"
2712 B=B-10-3*B1
2715 PRINT : PRINT : REM PRINT
2725 IF F>=13 THEN 2750
2730 GOTO 5060
2740 REM *** EATING ***
2750 GOSUB 7060
2755 PRINT : PRINT "DO YOU WANT TO EAT (1) POORLY (2) MOD-"
2760 PRINT "ERATELY OR (3) WELL";
2770 GOSUB 10000 : E = VAL(ZZ$)
2780 IF E>3 THEN 2755
2790 IF E<1 THEN 2755
2810 F=F-8-5*E
2820 IF F>=0 THEN 2860
2830 F=F+8+5*E
2840 PRINT : PRINT "YOU CAN'T EAT THAT WELL"
2850 GOTO 2755
2860 M=M+200+INT((A-220)/5)+ INT(RND(1)*10)
2870 L1=C1=0
2880 REM *** RIDERS ATTACK ***
2890 IF INT(RND(1)*10)>INT(((M/100-4)*(M/100-4)+72)/((M/100-4)*(M/100-4)+12)-1) THEN 3550
2900 PRINT : PRINT : PRINT "RIDERS AHEAD. THEY ";
2910 S5=0
2920 IF INT(RND(1)*10)<8 THEN 2950
2930 PRINT "DON'T ";
2940 S5=1
2950 PRINT "LOOK HOSTILE"
2955 PRINT : PRINT
2960 PRINT : PRINT "TACTICS"
2970 PRINT : PRINT "1.RUN   2.ATTACK   3.CONTINUE "
2975 PRINT "4.CIRCLE WAGONS"
2980 IF INT(RND(1)*10)>2 THEN 3000
2990 S5=1-S5
3000 GOSUB 10000: T1 = VAL(ZZ$)
3010 IF T1<1 OR T1>4 THEN 2970
3040 IF S5=1 THEN 3330
3050 IF T1>1 THEN 3110
3060 M=M+20
3070 M1=M1-15
3080 B=B-150
3090 A=A-40
3100 GOTO 3470
3110 IF T1>2 THEN 3240
3120 GOSUB 6140
3130 B=B-B1*40-80
3140 IF B1>1 THEN 3170
3150 PRINT : PRINT "NICE SHOOTING -- YOU DROVE THEM OFF"
3160 GOTO 3470
3170 IF B1<=15 THEN 3220
3180 PRINT : PRINT "LOUSY SHOT -- YOU GOT KNIFED"
3190 K8=1
3200 PRINT : PRINT "YOU HAVE TO SEE OL' DOC BLANCHARD"
3210 GOTO 3470
3220 PRINT : PRINT "KINDA SLOW WITH YOUR COLT"
3230 GOTO 3470
3240 IF T1>3 THEN 3290
3250 IF INT(RND(1)*10)>7 THEN 3450
3260 B=B-150
3270 M1=M1-15
3280 GOTO 3470
3290 GOSUB 6140
3300 B=B-B1*30-80
3310 M=M-25
3320 GOTO 3140
3330 IF T1>1 THEN 3370
3340 M=M+15
3350 A=A-10
3360 GOTO 3470
3370 IF T1>2 THEN 3410
3380 M=M-5
3390 B=B-100
3400 GOTO 3470
3410 IF T1>3 THEN 3430
3420 GOTO 3470
3430 M=M-20
3440 GOTO 3470
3450 PRINT : PRINT "THEY DID NOT ATTACK"
3460 GOTO 3550
3470 IF S5=0 THEN 3500
3480 PRINT : PRINT "THE RIDERS WERE FRIENDLY, BUT"
3485 PRINT "YOU SHOULD CHECK FOR POSSIBLE LOSSES"
3490 GOTO 3550
3500 PRINT : PRINT "THE RIDERS WERE HOSTILE -- CHECK"
3505 PRINT "FOR LOSSES"
3510 IF B>=0 THEN 3550
3520 PRINT : PRINT "YOU RAN OUT OF BULLETS AND GOT"
3525 PRINT "MASSACRED BY THE RIDERS"
3530 GOTO 5180
3540 REM *** SELECTION OF EVENTS ***
3550 D1=0
3560 GOSUB 7060
3570 R1= INT(RND(1)*100)+1
3576 IF R1>6 THEN 3578
3577 GOTO 3660
3578 IF R1>11 THEN 3580
3579 GOTO 3700
3580 IF R1>13 THEN 3582
3581 GOTO 3740
3582 IF R1>15 THEN 3584
3583 GOTO 3790
3584 IF R1>17 THEN 3586
3585 GOTO 3820
3586 IF R1>22 THEN 3588
3587 GOTO 3850
3588 IF R1>32 THEN 3590
3589 GOTO 3880
3590 IF R1>35 THEN 3592
3591 GOTO 3960
3592 IF R1>37 THEN 3594
3593 GOTO 4130
3594 IF R1>42 THEN 3596
3595 GOTO 4190
3596 IF R1>44 THEN 3598
3597 GOTO 4220
3598 IF R1>54 THEN 3600
3599 GOTO 4290
3600 IF R1>64 THEN 3602
3601 GOTO 4340
3602 IF R1>69 THEN 3604
3603 GOTO 4560
3604 IF R1>95 THEN 4670
3605 GOTO 4610
3660 PRINT : PRINT "WAGON BREAKS DOWN -- LOSE TIME AND"
3665 PRINT "SUPPLIES FIXING IT"
3670 M=M-15- INT(RND(1)*5)
3680 M1=M1-8
3690 GOTO 4710
3700 PRINT : PRINT "OX INJURES LEG -- SLOWS YOU DOWN"
3705 PRINT "FOR THE REST OF THE TRIP"
3710 M=M-25
3720 A=A-20
3730 GOTO 4710
3740 PRINT : PRINT "BAD LUCK -- YOUR DAUGHTER BROKE HER ARM"
3745 PRINT "YOU HAD TO STOP AND USE SUPPLIES TO"
3748 PRINT "MAKE A SLING"
3760 M=M-5- INT(RND(1)*4)
3770 M1=M1-2- INT(RND(1)*3)
3780 GOTO 4710
3790 PRINT : PRINT "OX WANDERS OFF -- LOSE TIME SEARCHING"
3800 M=M-17
3810 GOTO 4710
3820 PRINT : PRINT "YOUR SON GETS LOST -- IT TAKES HALF"
3825 PRINT "THE DAY TO FIND HIM"
3830 M=M-10
3840 GOTO 4710
3850 PRINT : PRINT "UNSAFE WATER -- LOSE TIME HUNTING"
3855 PRINT "FOR A CLEAN SPRING"
3860 M=M-2- INT(RND(1)*10)
3870 GOTO 4710
3880 IF M>950 THEN 4490
3890 PRINT : PRINT "HEAVY RAINS -- TIME AND SUPPLIES LOST"
3910 F=F-10
3920 B=B-500
3930 M1=M1-15
3940 M=M-5- INT(RND(1)*10)
3950 GOTO 4710
3960 PRINT : PRINT "BANDITS ATTACK"
3970 GOSUB 6140
3980 B=B-20*B1
3990 IF B>=0 THEN 4030
4000 PRINT : PRINT "YOU RAN OUT OF AMMO -- THEY ROBBED YOU"
4010 T=INT(T/3)
4020 GOTO 4040
4030 IF B1<=1 THEN 4100
4040 PRINT : PRINT "YOU GOT SHOT IN THE LEG AND THEY TOOK"
4050 PRINT "ONE OF YOUR OXEN. BETTER HAVE A DOC"
4060 PRINT "LOOK AT YOUR WOUND"
4065 K8=1
4070 M1=M1-5
4080 A=A-20
4090 GOTO 4710
4100 PRINT : PRINT "QUICKEST DRAW OUTSIDE OF DODGE CITY"
4110 PRINT "YOU GOT 'EM"
4120 GOTO 4710
4130 PRINT : PRINT "THERE WAS A FIRE IN YOUR WAGON -- "
4135 PRINT "FOOD AND SUPPLIES DAMAGED"
4140 F=F-40
4150 B=B-400
4160 M1=M1-3- INT(RND(1)*8)
4170 M=M-15
4180 GOTO 4710
4190 PRINT : PRINT "LOSE YOUR WAY IN HEAVY FOG -- TIME LOST"
4200 M=M-10- INT(RND(1)*5)
4210 GOTO 4710
4220 PRINT : PRINT "YOU KILLED A POISONOUS SNAKE -- BUT"
4225 PRINT "ONLY AFTER IT BIT YOU"
4230 B=B-10
4240 M1=M1-5
4250 IF M1>=0 THEN 4710
4260 PRINT : PRINT "YOU DIE OF THE SNAKEBITE SINCE YOU"
4265 PRINT "HAVE NO MEDICINE"
4270 GOTO 5180
4290 PRINT : PRINT "WAGON SWAMPED AT RIVER FORD --"
4295 PRINT "YOU LOSE FOOD AND CLOTHING"
4300 F=F-30
4310 C=C-20
4320 M=M-20- INT(RND(1)*20)
4330 GOTO 4710
4340 PRINT : PRINT "WILD ANIMALS ATTACK"
4350 GOSUB 6140
4360 IF B>39 THEN 4410
4370 PRINT : PRINT "YOU WERE TOO LOW ON BULLETS --"
4380 PRINT "THE WOLVES OVERPOWERED YOU"
4390 K8=1
4400 GOTO 5120
4410 IF B1>2 THEN 4440
4420 PRINT : PRINT "NICE SHOOTING -- THEY DIDN'T GET MUCH"
4430 GOTO 4450
4440 PRINT : PRINT "SLOW ON THE DRAW -- THEY GOT AT YOUR"
4445 PRINT "FOOD AND CLOTHES"
4450 B=B-20*B1
4460 C=C-4*B1
4470 F=F-8*B1
4480 GOTO 4710
4490 PRINT : PRINT "COLD WEATHER -- BRRRRRR! -- YOU"
4495 PRINT
4500 IF C>22+ INT(RND(1)*4) THEN 4530
4510 PRINT "DON'T ";
4520 C1=1
4530 PRINT "HAVE ENOUGH CLOTHING TO KEEP WARM"
4540 IF C1=0 THEN 4710
4550 GOTO 6300
4560 PRINT : PRINT "HAIL STORM -- SUPPLIES DAMAGED"
4570 M=M-5- INT(RND(1)*10)
4580 B=B-200
4590 M1=M1-4- INT(RND(1)*3)
4600 GOTO 4710
4610 IF E=1 THEN 6300
4620 IF E=3 THEN 4650
4630 IF INT(RND(1)*100)>25 THEN 6300
4640 GOTO 4710
4650 IF INT(RND(1)*10)<5 THEN 6300
4660 GOTO 4710
4670 PRINT : PRINT "HELPFUL INDIANS SHOW YOU WHERE TO"
4675 PRINT "FIND MORE FOOD"
4680 F=F+14
4690 GOTO 4710
4700 REM *** MOUNTAINS ***
4710 IF M<=950 THEN 1230
4720 IF INT(RND(1)*10)>9-INT(((M/100-15)*(M/100-15)+72)/((M/100-15)*(M/100-15)+12)) THEN 4860
4730 PRINT : PRINT "RUGGED MOUNTAINS"
4740 IF INT(RND(1)*10)>1 THEN 4780
4750 PRINT : PRINT "YOU GOT LOST -- LOSE VALUABLE TIME"
4755 PRINT "TRYING TO FIND THE TRAIL!"
4760 M=M-60
4770 GOTO 4860
4780 IF INT(RND(1)*100)>11 THEN 4840
4790 PRINT : PRINT "WAGON DAMAGED -- LOSE TIME AND SUPPLIES"
4800 M1=M1-5
4810 B=B-200
4820 M=M-20- INT(RND(1)*30)
4830 GOTO 4860
4840 PRINT : PRINT "THE GOING GETS SLOW"
4850 M=M-45- INT(RND(1)*50)
4860 IF F1=1 THEN 4900
4870 F1=1
4880 IF INT(RND(1)*10)<8 THEN 4970
4890 PRINT : PRINT "YOU MADE IT SAFELY THROUGH SOUTH"
4895 PRINT "PASS -- NO SNOW"
4900 IF M<1700 THEN 4940
4910 IF F2=1 THEN 4940
4920 F2=1
4930 IF INT(RND(1)*10)<7 THEN 4970
4940 IF M>950 THEN 1230
4950 M9=1
4960 GOTO 1230
4970 PRINT : PRINT "BLIZZARD IN MOUNTAIN PASS --"
4975 PRINT "TIME AND SUPPLIES LOST"
4980 L1=1
4990 F=F-25
5000 M1=M1-10
5010 B=B-300
5020 M=M-30- INT(RND(1)*40)
5030 IF C<18+ INT(RND(1)*2) THEN 6330
5040 GOTO 4940
5050 REM *** DYING ***
5060 PRINT : PRINT "YOU RAN OUT OF FOOD"
5062 GOSUB 7120
5065 PRINT : PRINT "AND STARVED TO DEATH"
5070 GOTO 5180
5080 T=0
5090 PRINT : PRINT "YOU CAN'T AFFORD A DOCTOR"
5100 GOTO 5120
5110 PRINT : PRINT "YOU RAN OUT OF MEDICAL SUPPLIES"
5115 GOSUB 7120
5120 PRINT : PRINT "YOU DIED OF ";
5130 IF K8=1 THEN 5160
5140 PRINT "PNEUMONIA"
5150 GOTO 5180
5160 PRINT "YOUR INJURIES"
5180 PRINT "DUE TO YOUR UNFORTUNATE SITUATION,"
5190 PRINT "THERE ARE A FEW FORMALITIES WE MUST"
5195 PRINT "GO THROUGH"
5200 PRINT
5210 PRINT "WOULD YOU LIKE A MINISTER? "
5220 GOSUB 10000: C$ = ZZ$
5230 PRINT "WOULD YOU LIKE A FANCY FUNERAL? "
5240 GOSUB 10000: C$ = ZZ$
5250 PRINT "WOULD YOU LIKE US TO INFORM YOUR "
5255 PRINT "NEXT OF KIN?"
5260 GOSUB 10000: C$ = ZZ$
5270 IF C$="YES" THEN 5310
5280 PRINT "BUT YOUR AUNT SADIE IN ST.LOUIS IS"
5290 PRINT "REALLY WORRIED ABOUT YOU."
5300 GOTO 5320
5310 PRINT "THAT WILL BE $4.50 FOR THE TELEGRAPH FEE"
5320 PRINT : PRINT
5325 FOR I=1 TO 1000: NEXT I
5328 PRINT
5330 PRINT "WE THANK YOU FOR THIS INFORMATION AND"
5340 PRINT "WE ARE SORRY YOU DIDN'T MAKE IT TO THE"
5350 PRINT "GREAT TERRITORY OF OREGON."
5360 PRINT "BETTER LUCK NEXT TIME."
5370 PRINT
5380 PRINT TAB(20);"SINCERELY"
5382 PRINT
5385 PRINT " THE OREGON CITY"
5390 PRINT " CHAMBER OF COMMERCE"
5395 PRINT : PRINT
5400 IF INKEY$="" THEN 5400
5401 END
5420 REM *** FINAL TURN ***
5430 F9=INT((2040-M2)/(M-M2))
5440 F=F+(1-F9)*(8+5*E)
5470 PRINT " YOU FINALLY ARRIVED AT OREGON CITY"
5480 PRINT : PRINT " AFTER 2040 LONG MILES -- HOORAY!!!"
5490 PRINT " A REAL PIONEER"
5500 PRINT
5510 F9=F9*14
5520 D3=D3*14+F9
5530 F9=F9+1
5540 IF F9<8 THEN 5560
5550 F9=F9-7
5560 IF F9=1 THEN D$="MONDAY"
5570 IF F9=2 THEN D$="TUESDAY"
5580 IF F9=3 THEN D$="WEDNESDAY"
5590 IF F9=4 THEN D$="THURSDAY"
5600 IF F9=5 THEN D$="FRIDAY"
5610 IF F9=6 THEN D$="SATURDAY"
5620 IF F9=7 THEN D$="SUNDAY"
5630 IF D3>124 THEN 5740
5710 D3=D3-93
5720 PRINT D$;" JULY ";D3;" 1847"
5730 GOTO 5920
5740 IF D3>155 THEN 5780
5750 D3=D3-124
5760 PRINT D$;" AUGUST ";D3;" 1847"
5770 GOTO 5920
5780 IF D3>185 THEN 5820
5790 D3=D3-155
5800 PRINT D$;" SEPTEMBER ";D3;" 1847"
5810 GOTO 5920
5820 IF D3>216 THEN 5860
5830 D3=D3-185
5840 PRINT D$;" OCTOBER ";D3;" 1847"
5850 GOTO 5920
5860 IF D3>246 THEN 5900
5870 D3=D3-216
5880 PRINT D$;" NOVEMBER ";D3;" 1847"
5890 GOTO 5920
5900 D3=D3-246
5910 PRINT D$;" DECEMBER ";D3;" 1847"
5920 PRINT
5930 GOSUB 7060
6050 FOR I=1 TO 2000: NEXT I
6080 PRINT TAB(2);"PRESIDENT JAMES K.POLK SENDS YOU HIS"
6090 PRINT : PRINT TAB(8);"HEARTIEST CONGRATULATIONS"
6100 PRINT : PRINT
6110 PRINT TAB(3);"AND WISHES YOU A PROSPEROUS LIFE"
6120 PRINT : PRINT TAB(10);"AT YOUR NEW HOME"
6125 IF INKEY$="" THEN 6125
6126 END
6130 REM *** SHOOTING SUBROUTINE ***
6140 S6= INT(RND(1)*10)
6150 PRINT "TYPE ";S6
6160 TT1=TIMER
6170 Z$=INKEY$:IF Z$<"0" OR Z$>"9" THEN 6170
6180 TT2=TIMER: I=TT2-TT1: Z=VAL(Z$)
6220 B1=INT(I*9-D9-5)
6230 PRINT
6240 IF B1>0 THEN 6260
6250 B1=0
6260 IF Z=S6 THEN 6280
6270 B1=9
6280 RETURN
6290 REM *** ILLNESS SUBROUTINE ***
6300 IF INT(RND(1)*100)<10+35*(E-1) THEN 6370
6310 Y=INT(RND(1)*100): GOSUB 9000:REM FOR NO POW FUNCTION
6320 PRINT : PRINT "SERIOUS ILLNESS -- "
6330 PRINT "YOU MUST STOP FOR MEDICAL ATTENTION"
6340 M1=M1-10
6350 S4=1
6360 GOTO 6440
6370 PRINT : PRINT "MILD ILLNESS -- MEDICINE USED"
6380 M=M-5
6390 M1=M1-2
6400 GOTO 6440
6410 PRINT : PRINT "BAD ILLNESS -- MEDICINE USED"
6420 M=M-5
6430 M1=M1-5
6440 IF M1<0 THEN 5110
6450 IF L1=1 THEN 4940
6460 GOTO 4710
7000 REM *** SET TOP WINDOW ***
7050 PRINT TAB(5);"MONDAY MARCH 29 1847"
7060 PRINT: PRINT "YOUR TOTAL MILEAGE IS ";M
7070 PRINT: PRINT "FOOD";TAB(8);"AMMO";TAB(16);"CLOTH.";TAB(24);"MISC.";TAB(32);"CASH"
7075 PRINT MID$(STR$(F),2);TAB(8);MID$(STR$(B),2);TAB(16);C;TAB(24);M1;TAB(32);T
7080 PRINT: PRINT "* * * * * * * * * * * * * * * * * * * *":REM PRINT
7090 RETURN
7100 REM *** UPDATE ***
7110 PRINT TAB(23);M
7120 PRINT: PRINT "FOOD";TAB(8);"AMMO";TAB(16);"CLOTH.";TAB(24);"MISC.";TAB(32);"CASH"
7130 PRINT MID$(STR$(F),2);TAB(8);MID$(STR$(B),2);TAB(16);C;TAB(24);M1;TAB(32);T
7150 RETURN
7200 REM *** CHECK PURCHASES ***
7210 GOSUB 10000 : P = VAL(ZZ$)
7220 IF P<0 THEN 7290
7230 T=T-P
7240 IF T>=0 THEN 7290
7250 PRINT "YOU DON'T HAVE THAT MUCH -- KEEP YOUR"
7260 PRINT "SPENDING DOWN. YOU MISS YOUR CHANCE"
7270 PRINT "TO SPEND ON THAT ITEM."
7280 T=T+P
7285 P=0
7290 RETURN
8000 CLS : RETURN : REM FOR I=1 TO 24: PRINT : NEXT I : RETURN
9000 IF (E-1)=0 THEN E1=100-40
9010 IF (E-1)=1 THEN E1=100-INT(40/4)
9020 IF (E-1)=2 THEN E1=100-INT(40/(4*4))
9030 IF Y<E1 THEN 6410
9040 RETURN

10000 INPUT ZZ$: IF ZZ$<>"." THEN RETURN
10010 INPUT "QUIT: ARE YOU SURE";KK$: IF KK$="Y" OR KK$="S" OR KK$="." THEN END
10020 GOTO 10000
