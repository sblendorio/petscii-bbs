10 DIM Z(40)
100 PRINT "LUNAR LANDING SIMULATION"
200 PRINT "CREATIVE COMPUTING MORRISTOWN, NJ"
300 PRINT : PRINT "ADAPTED TO APPLE-I BY COREY COHEN"
800 PRINT "---------------------------------": PRINT
1000 INPUT "INSTRUCTIONS (Y OR N)",A$: IF A$="." THEN GOSUB 10000 : GOTO 1000
1100 IF A$="N" THEN 3900
1600 PRINT
2000 PRINT "YOU ARE LANDING ON THE MOON!!!"
2100 PRINT "YOU ARE 1000 FT ABOVE YOUR LANDING SPOT"
2150 PRINT "YOU HAVE DOWNWARD VELOCITY OF 50 FT/SEC"
2200 PRINT "YOU HAVE 150 UNITS OF FUEL REMAIN."
2260 PRINT
2400 PRINT "(1) AFTER EACH SECOND, HEIGHT, VELOCITY"
2500 PRINT "    AND FUEL WILL BE REPORTED." : PRINT
2600 PRINT "(2) AT THE '?', ENTER THE NUMBER OF"
2700 PRINT "    UNITS OF FUEL YOU WISH TO BURN"
2710 PRINT "    DURING THE NEXT SECOND." : PRINT
2800 PRINT "(3) EACH UNIT OF FUEL WILL SLOW YOUR"
2810 PRINT "    DESCENT BY 1 FOOT/SEC.": PRINT
3100 PRINT "(4) THE MAXIMUM THRUST IS 30 FEET/SEC"
3200 PRINT "    OR 30 UNITS OF FUEL PER SECOND." : PRINT
3300 PRINT "(5) THE DECENT ENGINE WILL SHUTDOWN"
3310 PRINT "    AUTOMATICALLY ON LUNAR CONTACT."
3850 PRINT:
3855 INPUT "PRESS ENTER"; ZZ$ : IF ZZ$="." THEN GOSUB 10000
3900 PRINT : PRINT "GOOD LUCK..."
4200 PRINT : PRINT
4300 SLEEP 2000
4350 REM
4400 FOR I=1 TO 40:Z(I)=0: NEXT I
4500 PRINT : FOR I=1 TO 24: PRINT : NEXT I
4550 T=0:H=1000:V=50:F=150:Z4=1:B=0:Y2=-1:M=2
4600 Y1=1:X1=40
4650 FOR I=1 TO 5: PRINT : NEXT I
4660 Z(Y1)=15-(H\66): IF Z(Y1)=0 THEN Z(Y1)=1
4700 CLS : PRINT TAB(4);"<APOLLO DIGBY ON-BOARD COMPUTER>": PRINT:Z4=Z4+1
4710 PRINT "SEC";TAB(6);"FEET";TAB(16);"SPEED";TAB(26);"FUEL"
4720 PRINT TAB(1);STR$(T);TAB(5);STR$(H);TAB(17);STR$(V);TAB(25);STR$(F)
4730 FOR I=1 TO 39
4740 IF I=Y1 THEN 4760
4750 PRINT "-";: GOTO 4780
4760 IF Z(I)>-1 THEN PRINT "+";
4770 IF Z(I)<0 THEN PRINT ABS(Z(I));
4780 NEXT I: PRINT ""
4800 IF Z(Y1)>M-1 THEN M=Z(Y1)+2
4810 FOR Y=1 TO 15
4820 IF Y>M THEN 4980
4870 PRINT "";: IF (Y1>40) THEN Y1=1
4880 FOR X=1 TO 40: IF Z(X)<>Y THEN 4900
4890 PRINT TAB(X);: IF X=Y1 THEN PRINT "^";
4892 IF X<>Y1 THEN PRINT ".";
4894 B1=1
4900 IF Z(X)=(Y-1) AND X=Y1 THEN GOTO 4920
4910 GOTO 4970
4920 PRINT TAB(X);
4930 IF B>0 AND B<11 THEN PRINT "'";
4940 IF B>10 AND B<21 THEN PRINT "*";
4950 IF B>20 AND B<31 THEN PRINT "#";
4970 NEXT X
4980 PRINT : NEXT Y
4990 FOR I=1 TO 40
5000 PRINT TAB(I);
5010 IF Z(I)>14 AND I=(T+1) THEN PRINT "%";
5020 IF Z(I)>14 AND I<>(T+1) THEN PRINT "=";
5030 IF Z(I)<1 THEN PRINT "-";
5040 IF Z(I)>0 AND Z(I)<15 THEN PRINT "=";
5050 NEXT I: PRINT
5060 Y1=Y1+1
5070 INPUT ZZ$: B=VAL(ZZ$): IF ZZ$="." THEN GOSUB 10000 : GOTO 5070
5100 IF B<0 THEN 6500
5200 IF B>30 THEN B=30
5300 IF B>F THEN B=F
5400 V1=V-B+5
5600 F=F-B
5700 H=H-(V+V1)\2
5800 IF H<=0 THEN 6700
5900 T=T+1
6000 V=V1
6100 IF F>0 THEN 4650
6150 IF B=0 AND Y2>-1 THEN 6300
6200 PRINT "**** OUT OF FUEL ****":Y2=Y1
6300 IF Y1>40 THEN Y1=1
6310 IF Y2=0 THEN Y2=1
6320 PRINT TAB(Y2);"^":Y2=Y2-1
6350 Z(Y1)=H\66
6500 B=0
6600 GOTO 5400
6700 IF B<>0 THEN 6720
6710 GOTO 6730
6720 FOR I=1 TO 10: PRINT : NEXT I
6730 FOR I=1 TO 39: IF I=(Y2+1) THEN PRINT "#";
6740 IF I<>Y1 THEN 6770
6750 IF Y2=-1 AND V<>0 THEN PRINT "#";
6760 IF Y2=-1 AND V=0 THEN PRINT "^";: GOTO 6770
6770 PRINT "=";
6780 NEXT I: PRINT
6790 PRINT : PRINT TAB(8);"***** CONTACT *****": PRINT
6800 H=H+(V1+V)\2
6900 IF B=5 THEN 7200
7000 T1=V*V+H*(10-2*B)
7010 O1=-1
7020 O2=1
7030 IF ABS(O2-O1)>1 THEN GOTO 7050
7040 GOTO 7080
7050 O1=O2
7060 O2=(O2+T1\O2)\2
7070 GOTO 7030
7080 D=(-V+O2)\(5-B)
7100 GOTO 7300
7200 D=H\V
7300 V1=V+(5-B)*D
7600 PRINT "TOUCHDOWN AT";T+D;"SECONDS."
7700 PRINT "LANDING VELOCITY =";V1;"FEET/SEC."
7800 PRINT F;"UNITS OF FUEL REMAINING."
7900 IF V1<>0 THEN 8100
8000 PRINT "CONGRATULATIONS! A PERFECT LANDING!!"
8050 PRINT "YOUR LICENSE WILL BE RENEWED.... LATER."
8060 PRINT "YOUR SCORE IS";(F*Z4)
8100 IF ABS(V1)<2 THEN 8400
8200 PRINT "***** SORRY, BUT YOU BLEW IT!!!!"
8300 PRINT "CONDOLENCES WILL BE SENT TO YOUR NEXT"
8310 PRINT "OF KIN."
8350 PRINT "YOUR SCORE IS";Z4
8400 PRINT : PRINT : PRINT
8500 INPUT "ANOTHER MISSION",A$
8600 IF A$="Y" THEN 3900
8700 PRINT : PRINT "CONTROL OUT." : PRINT
8990 END
10000 INPUT "QUIT: ARE YOU SURE";KK$
10010 IF KK$<>"Y" AND KK$<>"S" AND KK$<>"." THEN RETURN
10020 END
